#include "include/Include_Misc.ulp";
#include "include/Include_Language.ulp";
#include "include/Include_Config.ulp";
#include "include/Include_Boardsize.ulp";

void debugMsg(string msg){
	//to prevent debug messages to apear in releace
	dlgMessageBox(msg);
}

void debugVal(string msg, int val){
	sprintf(msg,"%s%d",msg,val);
	debugMsg(msg);
}


void detectDrillsymbols(UL_BOARD B){
	char cDrillSymbols = 1;
	B.holes(H) {
		if(!H.drillsymbol){
			cDrillSymbols=0;
		}
	}
	B.signals(S){
		S.vias(V){
			if(!V.drillsymbol){
				cDrillSymbols=0;
			}
		}
	}
	B.elements(E) {
		E.package.contacts(C) {
			if(C.pad && (!C.pad.drillsymbol)){
				cDrillSymbols=0;
			}
		}
		E.package.holes(H) {
			if(!H.drillsymbol){
				cDrillSymbols=0;
			}
		}
	}

	if(!cDrillSymbols){
		int opt=dlgMessageBox(lanGetText("dlg.DrillError"),lanGetText("button.continue"),lanGetText("button.cancel"));
		
		if(opt){
			exit("");
		}
	}
}

string placeFrame(){
	string cmd = "Add ";
	getBoardSize();

	if(u2mm(max_x - min_x)<155) {		//adding an A4 frame for boars smaler then 155mm to fitt multiple mesurments
		cmd+="'FRAME-A4@" + filedir(argv[0]) + "xdevelop-frame.lbr' ";

		real offset=0.0;
		if(u2mm(max_y - min_y)>80){
			offset = u2mm(max_y - min_y) - 80;
		}

		sprintf(cmd, "%s(%fmm %fmm)", cmd, u2mm((max_x + min_x)/2), (u2mm((max_y + min_y)/2) - offset));
	}
	else{							//adding an A3 frame
		cmd+="'FRAME-A3@" + filedir(argv[0]) + "xdevelop-frame.lbr' ";
		sprintf(cmd, "%s(%fmm %fmm)", cmd, (u2mm((max_x + min_x)/2)-195), (u2mm((max_y + min_y)/2)-185));
		}

	return cmd+"; ";
}

void run(int runOrder){
	string cmd="";
	loadConfig();

	lanLoadText(cfg_textLanguage);

	//getting the filename to export/readout the dru
	string druname = "";
	project.board(B){
		druname=filesetext(B.name,"-exportpro.dru");
	}

	if (runOrder==0){ //execution #1
		//not file type specific settings 
		dlgMessageBox(lanGetText("dlg.setup.expl"));

		//checking if pdftk is installed
		if(system("CMD.EXE /C " + filedir(argv[0]) + "bin/CheckPDFTK.bat")==-1){
			//todo put text in language file
			int opt = dlgMessageBox(":To merge the pdf-files we use pdftk_server.<br>"+
							"You can get this tool <a href=\"https://www.pdflabs.com/tools/pdftk-server/\">here</a>.",
							lanGetText("button.continue"),lanGetText("button.cancel"));
			if (opt){
				exit("");
			}
		}

		//Check if printer options are correct
		if (cfgget("EAGLE:Printer.Border.Top")!="50000" ||
			cfgget("EAGLE:Printer.Border.Bottom")!="50000" ||
			cfgget("EAGLE:Printer.Border.Right")!="50000" ||
			cfgget("EAGLE:Printer.Border.Left")!="200000"||
			cfgget("EAGLE:Printer.Caption")!="0"){

			int opt = dlgMessageBox(lanGetText("dlg.setup.editPrintSettings"), lanGetText("general.yes"), lanGetText("general.no"));

			if(opt == 0){
				cfgset("EAGLE:Printer.Border.Top","50000");
				cfgset("EAGLE:Printer.Border.Bottom","50000");
				cfgset("EAGLE:Printer.Border.Right","50000");
				cfgset("EAGLE:Printer.Border.Left","200000");
				cfgset("EAGLE:Printer.Caption","0");
			}
		}

		if(schematic){
			cmd += "EDIT .brd; ";
		}

		cmd += "DRC SAVE \'" + druname + "\'; ";

	}
	else if (runOrder == 1){ //execution #2
		//Settings that only can be edited in the .brd file
		project.board(B){
			detectDrillsymbols(B);

			//detecting if all Fabdoc layers are visible
			B.layers(L){
				if (153<=L.number && L.number<=155 && !L.visible){
					sprintf(cmd, "%sLAYER %d FabDoc%d;", cmd, L.number, (L.number - 152));
				}
			}
			
			//todo: check ob die fabdoc layer schon benuzt werden

			//checking if frame is placed
			char isFrameAdded = 0;
			B.elements(E){
				if(strstr(strupr(E.package.name),"FRAME")!=-1){
				    isFrameAdded = 1;
				}
			}

			if (!isFrameAdded){
				int opt = dlgMessageBox(lanGetText("dlg.setup.noFrame"),lanGetText("general.yes"), lanGetText("general.manual"), lanGetText("button.continue"));
				if(opt == 0) {
					cmd += placeFrame();
				}
				else if (opt == 1){
					cmd += "add 'FRAME*@"+filedir(argv[0])+"/xdevelop-frame.lbr'";
					exit(cmd);
				}
			}
		}

		//checking if the default dru file is loaded
		if(filesize(druname)) {
			string data;
			fileread(data, druname);

			if ((strstr(data, "Eurocircuits-EAGLE design rules")!=-1)||
				(strstr(data, "Standard-Design-Regeln")!=-1)){
				int opt = dlgMessageBox(lanGetText("dlg.setup.stdDRU"), lanGetText("general.yes"), lanGetText("button.continue"));

				if (opt==0){
					cmd += "DRC *;";
					exit(cmd);
				}
			}
		}

		cmd+="EDIT .sch;";

		//Settings that only can be edited in the .sch file
		//toDo: überprüfen ob stückliste vorhanden

	}


	if(runOrder<1){
		sprintf(cmd, "%sRUN \'%s\' %d", cmd, argv[0], (++runOrder));
 	}

 	debugVal(cmd+"\n\nRUN Nr. ",runOrder);
	exit(cmd);
}

run(strtol(argv[1]));